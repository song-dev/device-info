import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'com.google.gms.google-services'

def PRODUCT_NAME = "device_info_"
def DATE = new SimpleDateFormat('yyyyMMdd').format(new Date())

android {
    compileSdkVersion COMPILE_SDK_VERSION as int
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId "com.song.deviceinfo"
        minSdkVersion MIN_SDK_VERSION as int
        targetSdkVersion TARGET_SDK_VERSION as int
        versionCode VERSION_CODE as int
        versionName VERSION_NAME
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            // 设置支持的SO库架构
            abiFilters 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'//, 'armeabi'
        }

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    android {
        lintOptions {
            abortOnError false
        }
    }

    repositories {
        flatDir {
            dirs 'libs'
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFileName
            if (outputFile != null && outputFile.endsWith('.apk')) {
                if ('debug' == variant.buildType.name) {
                    output.outputFileName = "${PRODUCT_NAME}${defaultConfig.versionName}_${DATE}" + "_debug.apk"
                } else if ('release' == variant.buildType.name) {
                    output.outputFileName = "${PRODUCT_NAME}${defaultConfig.versionName}_${DATE}" + ".apk"
                }
            }
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.navigation:navigation-fragment:2.3.2'
    implementation 'androidx.navigation:navigation-ui:2.3.2'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.preference:preference:1.1.1'
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation "androidx.multidex:multidex:2.0.1"

    implementation 'com.squareup.okhttp3:okhttp:4.7.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'

    implementation 'com.jakewharton:butterknife:10.2.3'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.3'

    implementation 'com.tencent.bugly:crashreport:3.2.33'
    implementation 'com.tencent.bugly:nativecrashreport:3.7.1'

    implementation 'com.google.firebase:firebase-core:18.0.0'
    implementation 'com.google.firebase:firebase-analytics:18.0.0'
    implementation 'com.google.firebase:firebase-perf:19.0.10'

    //OAID
    implementation(name: 'miit_mdid_1.0.10', ext: 'aar')

    debugImplementation 'com.didichuxing.doraemonkit:doraemonkit:3.1.5'
    releaseImplementation 'com.didichuxing.doraemonkit:doraemonkit-no-op:3.1.5'

}
